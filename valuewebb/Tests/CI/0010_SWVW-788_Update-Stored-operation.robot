*** Settings ***
Documentation    Test Case 010 SWVW-788. This Test Case is used to update and submit Stored Transactions
Library     SeleniumLibrary
Library     BuiltIn
Library     OperatingSystem
Resource    ../../Resources//ERC_Module//ERC_Setup_Teardown.robot
Resource    ../../Resources//ERC_Module/Page_Objects/TT_Document_Page.robot
Resource    ../../Resources//ERC_Module/Page_Objects/SearchTT_Requests_Page.robot
Resource    ../../Resources/ERC_Module/Page_Objects/Login_Page.robot
Suite Setup     User to Open Browser
Suite Teardown    User to Close Browser

*** Variables ***
${TC010_ALERT_MESSAGE_VERIFIED}     Document is verified!
${TC010_ALERT_MESSAGE_UPDATE}       Update operation is done.
${TC010_TAXPAYER_CODE}              5604172W
${TC010_EXP_NAME}                   HMR Trading
${TC010_EXP_ADDRESS}                HY Singa St. Avenue Cmp
${TC010_EXP_CITY}                   New Delhun
${TC010_COUNTRY}                    CI
${TC010_DEC_CODE}                   00069Z
${TC010_CONTACT}                    Edmundo
${TC010_PHONE}                      +22990012356
${TC010_EMAIL}                      dan.bacarra@webbfontaine.com
${TC010_TYPE}                       Full
${TC010_CARRIER}                    AA
${TC010_AW_BL_CM_NO}                LH02202
${TC010_DATE}                       01/01/2020
${TC010_FL_VO_TR_NO}                PR-256
${TC010_TRANSPORT_NO}               FERM EXPRESS
${TC010_MOT}                        XPR
${TC010_NET_MASS}                   10.00
${TC010_GROSS_MASS}                 10.00
${TC010_PLACE_LOADING}              CIABO
${TC010_PLACE_DISCHARGE}            CIABJ
${TC010_TOTAL_PACKING}              10
${TC010_INV_REF}                    678-110
${TC010_INC}                        FOB
${TC010_CURRENCY}                   XOF
${TC010_VALUE}                      2,000.00
${TC010_MOP}                        10
${TC010_NO_OF_ITEM}                 2
${TC010_AUTOGENERATED}              AUTO
${TC010_HSC}                        3002901000
${TC010_QTY}                        5.00
${TC010_UOM}                        Kg
${TC010_FF}                         Facture Finale
${TC010_FF_LOCATOR}                 //td[contains(text(), 'Facture Finale')]
${TC010_TRANSPORT_DOCUMENT}         Transport Document
${TC010_TD_LOCATOR}                 //td[contains(text(), 'Transport Document')]
${TC010_DAF}                        DAF
${TC010_DAF_LOCATOR}                //td[contains(text(), 'DAF')]
${TC010_STATUS}                     Stored
${TC010_WAITING_TIMEOUT}            20s


*** Test Cases ***
Test Case 010 SWVW-788 Update Stored operation
    [Tags]    Smoke
    Given Login at ERC Homepage
    When User to change the French Translation to English
    And In Search TT Request page, select Stored option in Status dropdown then click Search button
    And Click Edit icon beside Status column
    And Verify the following buttons on Edit TT page
    And Verify following fields that can be updated/edited on Header Tab
    And Verify following fields that can be updated/edited on Names and Parties Tab
    And Verify following fields that can be updated/edited on Transport Tab
    And Verify following fields that can be updated/edited on Financial Tab
    And Verify following fields that can be updated/edited on Goods Tab
    And Verify following fields that can be updated/edited on Remarks Tab
    And Verify that on Attachments Tab, file can be added and uploaded
    And Verify that when fields has been successfully updated and Update button has been clicked
    Then Verify all changes has been reflected in all fields

*** Keywords ***
# TODO separate keyword for getting values for select dropdown and modifying COO in HSC

User to change the French Translation to English
    Login_Page.ERC Page should be in English
    Login_Page.Detect Language used in ERC Page

In Search TT Request page, select Stored option in Status dropdown then click Search button
    SearchTT_Requests_Page.Select Status of Transaction    ${TC010_STATUS}
    SearchTT_Requests_Page.Click Search button

Click Edit icon beside Status column
    SearchTT_Requests_Page.Click Edit Icon button

Verify the following buttons on Edit TT page
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_VERIFY_BTN}            timeout=${TC010_WAITING_TIMEOUT}
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_UPDATE_STORED_BTN}     timeout=${TC010_WAITING_TIMEOUT}
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_SUBMIT_BTN}            timeout=${TC010_WAITING_TIMEOUT}
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_DELETE_BTN}            timeout=${TC010_WAITING_TIMEOUT}
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_CLOSE_BTN}             timeout=${TC010_WAITING_TIMEOUT}

Verify following fields that can be updated/edited on Header Tab
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_FDI-DAI_NO}       timeout=${TC010_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${ERC_DOCPAGE_FDI-DAI_NO}       timeout=${TC010_WAITING_TIMEOUT}
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_FDI-DAI_DATE}     timeout=${TC010_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${ERC_DOCPAGE_FDI-DAI_DATE}     timeout=${TC010_WAITING_TIMEOUT}
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_UCR}              timeout=${TC010_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${ERC_DOCPAGE_UCR}              timeout=${TC010_WAITING_TIMEOUT}
    
Verify following fields that can be updated/edited on Names and Parties Tab
    TT_Document_Page.Click Names & Parties Tab
    TT_Document_Page.Enter/Modify Field Details of      xpath=${ERC_DOCPAGE_SUGGEST_IMP}    ${TC010_TAXPAYER_CODE}
    TT_Document_Page.Enter/Modify Field Details of      xpath=${ERC_DOCPAGE_INPUT_EXPNAM}   ${TC010_EXP_NAME}
    TT_Document_Page.Enter/Modify Field Details of      xpath=${ERC_DOCPAGE_INPUT_EXPADD}   ${TC010_EXP_ADDRESS}
    TT_Document_Page.Enter/Modify Field Details of      xpath=${ERC_DOCPAGE_INPUT_EXPCTY}   ${TC010_EXP_CITY}
    TT_Document_Page.Enter/Modify Field Details of      xpath=${ERC_DOCPAGE_SUGGEST_COUNTRY}  ${TC010_COUNTRY}
    TT_Document_Page.Enter/Modify Field Details of      xpath=${ERC_DOCPAGE_SUGGEST_DEC}      ${TC010_DEC_CODE}
    TT_Document_Page.Enter/Modify Field Details of      xpath=${ERC_DOCPAGE_INPUT_CONTACT}    ${TC010_CONTACT}
    TT_Document_Page.Enter/Modify Field Details of      xpath=${ERC_DOCPAGE_INPUT_PHONENUM}   ${TC010_PHONE}
    TT_Document_Page.Enter/Modify Field Details of      xpath=${ERC_DOCPAGE_INPUT_APPEMAIL}   ${TC010_EMAIL}
    
Verify following fields that can be updated/edited on Transport Tab
    TT_Document_Page.Click Transport Tab
    TT_Document_Page.Select Shipment Type   ${TC010_TYPE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_COS}      ${TC010_COUNTRY}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_CARRIER}  ${TC010_CARRIER}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_BLNO}       ${TC010_AW_BL_CM_NO}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_DAT_BLDATE}       ${TC010_DATE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_VOYAGENO}   ${TC010_FL_VO_TR_NO}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TRANSPORTNO}  ${TC010_TRANSPORT_NO}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_MOT}        ${TC010_MOT}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_NETMASS}      ${TC010_NET_MASS}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_GROSSMASS}    ${TC010_GROSS_MASS}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_PLACELOADING}  ${TC010_PLACE_LOADING}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_DISCHARGE}     ${TC010_PLACE_DISCHARGE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTAL_PACKING}   ${TC010_TOTAL_PACKING}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_MARKS}           ${TC010_AUTOGENERATED}
    
Verify following fields that can be updated/edited on Financial Tab
    TT_Document_Page.Click Financial Tab
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_INVREF}      ${TC010_INV_REF}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_DAT_INV}           ${TC010_DATE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_CURRENCY}  ${TC010_CURRENCY}
    Select Incoterm    ${TC010_INC}
    Select Incoterm per Item    ${TC010_INC}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTALFOB}          ${TC010_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTALCHARGES}      ${TC010_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTALFREIGHT}      ${TC010_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTALINSURANCE}    ${TC010_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTALDEDUCTIBLES}  ${TC010_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_TOTALINV}        ${TC010_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_PAYMENT}         ${TC010_MOP}
    TT_Document_Page.Verify Field and Value of         xpath=${ERC_DOCPAGE_INPUT_TOTALCFR}
    TT_Document_Page.Verify Field and Value of         xpath=${ERC_DOCPAGE_INPUT_TOTALCIF}
    TT_Document_Page.Verify Field and Value of         xpath=${ERC_DOCPAGE_INPUT_TOTALVALUE}
    TT_Document_Page.Verify Field and Value of         xpath=${ERC_DOCPAGE_INPUT_TOTALDUTIABLE}

Verify following fields that can be updated/edited on Goods Tab
    TT_Document_Page.Click Goods Tab
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_GDESC}         ${TC010_AUTOGENERATED}
    TT_Document_Page.Enter Product Code under Goods Tab     ${TC010_HSC}
    TT_Document_Page.Enter Product Description under Goods Tab  ${TC010_AUTOGENERATED}
    TT_Document_Page.Enter Incoterm Item value under Goods Tab  ${TC010_VALUE}
    TT_Document_Page.Enter Item Quantity under Goods Tab    ${TC010_QTY}
    TT_Document_Page.Enter Item Unit Measurement under Goods Tab    ${TC010_UOM}


Verify following fields that can be updated/edited on Remarks Tab
    TT_Document_Page.Click Remarks Tab
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_REMARKS}       ${TC010_AUTOGENERATED}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_REASONS}       ${TC010_AUTOGENERATED}

Verify that on Attachments Tab, file can be added and uploaded
    TT_Document_Page.Click Attachment Tab
    TT_Document_Page.Input Doc Type under Attachments Tab            ${TC010_FF}
    TT_Document_Page.Input Reference Number under Attachments Tab    ${TC010_AUTOGENERATED}
    TT_Document_Page.Upload and Add File under Attachments Tab
    TT_Document_Page.Input Doc Type under Attachments Tab            ${TC010_TRANSPORT_DOCUMENT}
    TT_Document_Page.Input Reference Number under Attachments Tab    ${TC010_AUTOGENERATED}
    TT_Document_Page.Upload and Add File under Attachments Tab
    TT_Document_Page.Input Doc Type under Attachments Tab            ${TC010_DAF}
    TT_Document_Page.Input Reference Number under Attachments Tab    ${TC010_AUTOGENERATED}
    TT_Document_Page.Upload and Add File under Attachments Tab

Verify that when fields has been successfully updated and Update button has been clicked
    [Documentation]     This keyword used to update transaction from processing document with confirmation message
    TT_Document_Page.Update operation for Stored/Queried

Verify all changes has been reflected in all fields
    TT_Document_Page.Click Names & Parties Tab
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_IMP}   ${TC010_TAXPAYER_CODE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_EXPNAM}  ${TC010_EXP_NAME}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_EXPADD}  ${TC010_EXP_ADDRESS}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_EXPCTY}  ${TC010_EXP_CITY}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_COUNTRY}  ${TC010_COUNTRY}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_DEC}      ${TC010_DEC_CODE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_CONTACT}    ${TC010_CONTACT}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_PHONENUM}   ${TC010_PHONE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_APPEMAIL}   ${TC010_EMAIL}
    TT_Document_Page.Click Transport Tab
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_COS}      ${TC010_COUNTRY}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_CARRIER}  ${TC010_CARRIER}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_BLNO}       ${TC010_AW_BL_CM_NO}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_DAT_BLDATE}       ${TC010_DATE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_VOYAGENO}   ${TC010_FL_VO_TR_NO}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_TRANSPORTNO}  ${TC010_TRANSPORT_NO}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_MOT}        ${TC010_MOT}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_NETMASS}      ${TC010_NET_MASS}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_GROSSMASS}    ${TC010_GROSS_MASS}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_PLACELOADING}  ${TC010_PLACE_LOADING}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_DISCHARGE}     ${TC010_PLACE_DISCHARGE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_TOTAL_PACKING}   ${TC010_TOTAL_PACKING}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_MARKS}           ${TC010_AUTOGENERATED}
    TT_Document_Page.Click Financial Tab
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_INVREF}      ${TC010_INV_REF}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_DAT_INV}           ${TC010_DATE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_CURRENCY}  ${TC010_CURRENCY}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_INC}       ${TC010_INC}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_INCITEM}   ${TC010_INC}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_TOTALFOB}    ${TC010_VALUE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_TOTALCHARGES}  ${TC010_VALUE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_TOTALFREIGHT}  ${TC010_VALUE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_TOTALINSURANCE}    ${TC010_VALUE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_TOTALDEDUCTIBLES}  ${TC010_VALUE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_TOTALINV}        ${TC010_VALUE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_PAYMENT}         ${TC010_MOP}
    TT_Document_Page.Click Goods Tab
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_GDESC}       ${TC010_AUTOGENERATED}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_HSCODE}    ${TC010_HSC}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_HSCDESC}     ${TC010_AUTOGENERATED}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_INCNUM}      ${TC010_VALUE}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_QTYNUM}      ${TC010_QTY}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_SUGGEST_UNIT}      ${TC010_UOM}
    TT_Document_Page.Click Remarks Tab
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_REMARKS}     ${TC010_AUTOGENERATED}
    Verify the entered details on the field was updated     xpath=${ERC_DOCPAGE_INPUT_REASONS}     ${TC010_AUTOGENERATED}
    TT_Document_Page.Click Attachment Tab
    Verify the uploaded attachment was updated      xpath=${TC010_FF_LOCATOR}   ${TC010_FF}
    Verify the uploaded attachment was updated      xpath=${TC010_TD_LOCATOR}   ${TC010_TRANSPORT_DOCUMENT}
    Verify the uploaded attachment was updated      xpath=${TC010_DAF_LOCATOR}  ${TC010_DAF}

Verify the entered details on the field was updated
    [Documentation]   This keyword is to verify details were updated on the fields after the operation
    [Arguments]  ${locator}     ${expectedString}
    ${value}=    Get Element Attribute  ${locator}   value
    Should Be Equal     ${value}    ${expectedString}


Verify the uploaded attachment was updated
   [Documentation]   This keyword is to verify uploaded files were updated on Attachment tab
   [Arguments]  ${locator}     ${expectedString}
   Wait Until Element is Visible    ${locator}     timeout=${ERC_DOCPAGE_WAITING_TIMEOUT}
   element should be visible   ${locator}
   ${value}=    Get Text  ${locator}
   Should Be Equal  ${value}    ${expectedString}

Select Incoterm
    [Documentation]     This keyword only used modify the previous Parent Incoterm
    [Arguments]     ${type}
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_SUGGEST_INC}  timeout=${ERC_DOCPAGE_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${ERC_DOCPAGE_SUGGEST_INC}  timeout=${ERC_DOCPAGE_WAITING_TIMEOUT}
    Clear Element Text  xpath=${ERC_DOCPAGE_SUGGEST_INC}
    Press Keys   xpath=${ERC_DOCPAGE_SUGGEST_INC}  TAB
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_OVERRIDE_YES_BTN}  timeout=${ERC_DOCPAGE_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${ERC_DOCPAGE_OVERRIDE_YES_BTN}  timeout=${ERC_DOCPAGE_WAITING_TIMEOUT}
    click link     xpath=${ERC_DOCPAGE_OVERRIDE_YES_BTN}
    input text     xpath=${ERC_DOCPAGE_SUGGEST_INC}  ${type}
    Press Keys     xpath=${ERC_DOCPAGE_SUGGEST_INC}  TAB

Select Incoterm per Item
    # TODO put verification in this keywords
    [Documentation]     This keyword only used modify the previous Incoterm per Item
    [Arguments]     ${type}
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_SUGGEST_INCITEM}  timeout=${ERC_DOCPAGE_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${ERC_DOCPAGE_SUGGEST_INCITEM}  timeout=${ERC_DOCPAGE_WAITING_TIMEOUT}
    Clear Element Text  xpath=${ERC_DOCPAGE_SUGGEST_INCITEM}
    Press Keys          xpath=${ERC_DOCPAGE_SUGGEST_INCITEM}  TAB
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_OVERRIDE_YES_BTN}  timeout=${ERC_DOCPAGE_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${ERC_DOCPAGE_OVERRIDE_YES_BTN}  timeout=${ERC_DOCPAGE_WAITING_TIMEOUT}
    click link     xpath=${ERC_DOCPAGE_OVERRIDE_YES_BTN}
    input text     xpath=${ERC_DOCPAGE_SUGGEST_INCITEM}  ${type}
    Press Keys     xpath=${ERC_DOCPAGE_SUGGEST_INC}  TAB
    click link     xpath=${ERC_DOCPAGE_OVERRIDE_YES_BTN}