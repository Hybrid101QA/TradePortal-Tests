*** Settings ***
Documentation    Test Case 001 SWVW-918. This Test Case is used for Submit Trade Transaction with loaded TVF
Library     SeleniumLibrary
Library     BuiltIn
Library     OperatingSystem
Library     DateTime
Resource    ../../Resources/ERC_Module/ERC_Setup_Teardown.robot
Resource    ../../Resources/ERC_Module/Page_Objects/TT_Document_Page.robot
Resource    ../../Resources/ERC_Module/Page_Objects/SearchTT_Requests_Page.robot
Resource    ../../Resources/TVF_Module/Page_Objects/Search_Page.robot
Resource    ../../Resources/TVF_Module/Page_Objects/Main_Page.robot
Resource    ../../Resources/ERC_Module/Page_Objects/Login_Page.robot
Resource    ../../Resources/TVF_Module/Page_Objects/TVF_Login_Page.robot
Suite Setup     User to Open Browser
Suite Teardown    User to Close Browser

*** Variables ***
${TC001_SLEEP_TIMEOUT}                    0.5s
${TC001_WAITING_TIMEOUT}                  20s
${TC001_IMPORT_XML_FULLPATH}              ${EXECDIR}/Resources/TVF_Module/Data/CI/TVF-11888.xml
${TC001_ATTACHMENTS_DOC_FILE}             ${EXECDIR}/Resources/TVF_Module/Data/CI/SingleDPIVReport.pdf
${TC001_HEADER}                           Fill Import TVF
${TC001_USER_REFERENCE}                   ERC
${TC001_DOM_REFERENCE}                    ERC
${TC001_IMPORT_XML_ERROR_MESSAGE_1}       User Reference is Mandatory
${TC001_MODAL_TITLE}                      Confirmation of Operation
${TC001_TESTCASE_REFERENCE}               001
${TC001_ATTACHMENTS_DOC_TYPE}             FACTURE
${TC001_ATTACHMENTS_DOC_DATE}             TODAY
${TC001_FDI_NO}                           883
${TC001_FDI_DATE}                         TODAY
${TC001_USER_CUSTOMS_OFFICE}              CIAB1
${TC001_TYPE}                             Partial
${TC001_INSURANCE_VAL}                    2,000.00
${TC001_ALERT_INVALID_DOC}                //a[contains(text(), 'Invalid Document Type')]
${TC001_FACTURE_DOC}                      //td[contains(text(), 'FACTURE' )]
${TC001_DOC_TYPE_SELECTOR}                //input[@id="docType"]
${TC001_SEARCH_BTN}                       //a[@href="/tvf/"]
${TC001_WAITING_TIMEOUT}                  5s
${TC001_STATUS}                           Stored
${TC001_CONTACT}                          Edmundo
${TC001_PHONE}                            +22990012356
${TC001_EXP_CTY}                          Pateros
${TC001_EMAIL}                            dan.bacarra@webbfontaine.com
${TC001_CARRIER}                          AA
${TC001_AW_BL_CM_NO}                      LH02202
${TC001_AW_BL_CM_DATE}                    01/01/2020
${TC001_FL_VO_TR_NO}                      PR-256
${TC001_TRANSPORT_NO}                     FERM EXPRESS
${TC001_NET_MASS}                         10
${TC001_GROSS_MASS}                       10
${TC001_PLACE_DISCHARGE}                  CIABJ
${TC001_TOTAL_PACKING}                    10
${TC001_FCL}                              0
${TC001_LCL}                              0
${TC001_INC}                              FOB
${TC001_INV_VALUE}                        5,000.00
${TC001_AUTOGENERATED}                    Auto-generated details
${TC001_FF}                               Facture Finale
${TC001_DAI}                              DAI
${TC001_TRANSPORT_DOCUMENT}               Transport Document
${TC001_DAF}                              DAF
${TC001_ALERT_SUBMITTED}                  Submit operation is done.
${TC001_STATUS_SUBMITTED}                 //span[contains(text(), 'Submitted')]



*** Test Cases ***
Test Case 001 SWVW-918 Request Submit operation Load TVF data
    [Tags]    Smoke
    [Setup]   Auto-generate of specific details
    Given User should already created transaction from TVF application with Validated status
    When Open the ERC Application
    And User to change the French Translation to English
    And Click Create TT link
    And Enter the created TVF record in FDI/DAI Number with FDI/DAI Date then click download icon button
    And Enter details on the following fields
    And Go to Attachment tab then remove the uploaded Facture file
    And Click Verify button
    And Click Store button
    And User open again the transaction in Search for Trade Transactions (TT)
    And Enter the details on the following mandatory fields
    And Click Submit button with Warning Message
    Then User should successfully submitted the transaction with Load TVF Data

*** Keywords ***
Auto-generate of specific details
    [Documentation]     This keyword used to generate unique details for User Reference
    ${current_date}=      Get Current Date             result_format=%H%M%S%y%m%d     exclude_millis=yes
    Set Suite Variable    ${TC001_USER_REFERENCE}       ${current_date}${TC001_TESTCASE_REFERENCE}
    Set Suite Variable    ${TC001_DOM_REFERENCE}        ${current_date}${TC001_TESTCASE_REFERENCE}

User should already created transaction from TVF application with Validated status
    [Documentation]  Test Step Precondition
    TVF_Login_Page.Login at TVF Homepage
    Sleep   ${TC001_SLEEP_TIMEOUT}
    TVF_Login_Page.TVF Page should be in English
    TVF_Login_Page.Detect Language used in TVF Page
    User to Import XML
    User to fill the mandatory fields prior to Validate operation
    User to Validate operation
    User to Domiciliate operation
    User to Authorize operation

User to Import XML
    [Documentation]     Test Step Precondition Sub Part 2
    Main_Page.Click Fill TVF Import        ${TC001_HEADER}
    Main_Page.Import XML File from         ${TC001_IMPORT_XML_FULLPATH}
    Wait Until Page Contains               ${TC001_IMPORT_XML_ERROR_MESSAGE_1}   timeout=${TC001_WAITING_TIMEOUT}
    Main_Page.Verify the Value of Customs Office under Header Tab    ${TC001_USER_CUSTOMS_OFFICE}

User to fill the mandatory fields prior to Validate operation
    [Documentation]     Test Step Precondition Sub Part 3
    Main_Page.Input User Reference under Header Tab         ${TC001_USER_REFERENCE}
    Main_Page.Click Names And Parties Tab
    TT_Document_Page.Enter/Modify Field Details of    xpath=${TVF_MAINPAGE_INPUT_IMPEMAIL}   ${TC001_EMAIL}
    TT_Document_Page.Enter/Modify Field Details of    xpath=${TVF_MAINPAGE_INPUT_EXPCTY}     ${TC001_EXP_CTY}
    TT_Document_Page.Enter/Modify Field Details of    xpath=${TVF_MAINPAGE_INPUT_EXPEMAIL}   ${TC001_EMAIL}
    Main_Page.Click Attachment Tab
    Input Doc Type under Attachments Tab                             ${TC001_ATTACHMENTS_DOC_TYPE}
    Main_Page.Input Doc Ref under Attachments Tab                    ${TC001_AUTOGENERATED}
    Main_Page.Input Doc Date under Attachments Tab                   ${TC001_ATTACHMENTS_DOC_DATE}
    Main_Page.Upload File under Attachments Tab                      ${TC001_ATTACHMENTS_DOC_FILE}


Input Doc Type under Attachments Tab
    [Documentation]           This Keyword is used to input Doc Type into the Attachement in CI
    [Arguments]               ${doc_code_input}
    Scroll Element Into View  xpath=${TC001_DOC_TYPE_SELECTOR}
    Set Focus to Element      xpath=${TC001_DOC_TYPE_SELECTOR}
    Sleep                     ${TC001_SLEEP_TIMEOUT}
    Input Text                xpath=${TC001_DOC_TYPE_SELECTOR}      ${doc_code_input}
    Press Keys                xpath=${TC001_DOC_TYPE_SELECTOR}      RETURN
    Press Keys                xpath=${TC001_DOC_TYPE_SELECTOR}      TAB

User to Validate operation
    [Documentation]     Test Step Precondition Sub Part 5
    Main_Page.Click Validate Button
    Main_Page.Click OK to Confirm Operation
    Main_Page.Check Validate Operation if Success
    Verify the TVF Number was generated

User to Domiciliate operation
    Wait Until Element is Visible   xpath=${TC001_SEARCH_BTN}   timeout=${TC001_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${TC001_SEARCH_BTN}   timeout=${TC001_WAITING_TIMEOUT}
    click element           xpath=${TC001_SEARCH_BTN}
    TT_Document_Page.Enter/Modify Field Details of       xpath=${TVF_SEARCHPAGE_BASIC_SEARCH_TVF_NUMBER}   ${TC001_FDI_NO}
    click element           xpath=${TVF_SEARCHPAGE_SEARCH_BTN}
    Wait Until Element is Visible   xpath=${TVF_MAINPAGE_EDIT_ICON}   timeout=${TC001_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${TVF_MAINPAGE_EDIT_ICON}   timeout=${TC001_WAITING_TIMEOUT}
    click element           xpath=${TVF_MAINPAGE_EDIT_DOCUMENT}
    Switch Window   NEW   timeout=${TC001_WAITING_TIMEOUT}
    Page Should Contain     ${TC001_FDI_NO}
    Sleep                   ${TC001_SLEEP_TIMEOUT}
    Main_Page.Click Financial Tab
    TT_Document_Page.Enter/Modify Field Details of    xpath=${TVF_MAINPAGE_FINANCIAL_TAB_DOM_REF}   ${TC001_DOM_REFERENCE}
    click element           xpath=${TVF_MAINPAGE_DOMICILIATE_BUTTON}
    Page Should Contain     ${TC001_MODAL_TITLE}
    click element           xpath=${TVF_MAINPAGE_VALIDATE_CONFIRM_OPER}
    Main_Page.Check Domiciliate Operation if Success
    Verify the TVF Number was generated

User to Authorize operation
    Wait Until Element is Visible   xpath=${TC001_SEARCH_BTN}   timeout=${TC001_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${TC001_SEARCH_BTN}   timeout=${TC001_WAITING_TIMEOUT}
    click element           xpath=${TC001_SEARCH_BTN}
    TT_Document_Page.Enter/Modify Field Details of       xpath=${TVF_SEARCHPAGE_BASIC_SEARCH_TVF_NUMBER}   ${TC001_FDI_NO}
    click element           xpath=${TVF_SEARCHPAGE_SEARCH_BTN}
    Wait Until Element is Visible   xpath=${TVF_MAINPAGE_EDIT_DOCUMENT}   timeout=${TC001_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${TVF_MAINPAGE_EDIT_DOCUMENT}   timeout=${TC001_WAITING_TIMEOUT}
    Set Focus to Element    xpath=${TVF_MAINPAGE_EDIT_DOCUMENT}
    Sleep                   ${TC001_SLEEP_TIMEOUT}
    click element           xpath=${TVF_MAINPAGE_EDIT_DOCUMENT}
    Switch Window   NEW   timeout=${TC001_WAITING_TIMEOUT}
    Page Should Contain     ${TC001_FDI_NO}
    Wait Until Element is Visible   xpath=${TVF_MAINPAGE_AUTHORIZE_BUTTON}   timeout=${TC001_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${TVF_MAINPAGE_AUTHORIZE_BUTTON}   timeout=${TC001_WAITING_TIMEOUT}
    Sleep                   ${TC001_SLEEP_TIMEOUT}
    click element           xpath=${TVF_MAINPAGE_AUTHORIZE_BUTTON}
    Page Should Contain     ${TC001_MODAL_TITLE}
    click element           xpath=${TVF_MAINPAGE_VALIDATE_CONFIRM_OPER}
    Main_Page.Check Authorize Operation if Success
    Verify the TVF Number was generated

Verify the TVF Number was generated
    [Documentation]   This keyword used to use the tvf number in eRC
    Main_Page.Click Header Tab
    ${TC001_FDI_NO}=        Main_Page.Get TVF Number from Header Tab
    Set Suite Variable       ${TC001_FDI_NO}       ${TC001_FDI_NO}

Open the ERC Application
    Execute Javascript    window.open('')
    Switch Window   NEW   timeout=${TC001_WAITING_TIMEOUT}
    Go To   ${GLOBAL_ERC_SERVER_URL}

User to change the French Translation to English
    Login_Page.ERC Page should be in English
    Login_Page.Detect Language used in ERC Page

Enter the created TVF record in FDI/DAI Number with FDI/DAI Date then click download icon button
    TT_Document_Page.Input FDI/DAI Number under Header Tab     ${TC001_FDI_NO}
    TT_Document_Page.Input FDI/DAI Date and click Import icon button
    Sleep   ${TC001_SLEEP_TIMEOUT}

Enter details on the following fields
    TT_Document_Page.Click Transport Tab
    TT_Document_Page.Select Shipment Type    ${TC001_TYPE}

Go to Attachment tab then remove the uploaded Facture file
    [Documentation]     This keyword is to delete the uploaded file in Attachment tab
    Click to view the invalid document
    Wait Until Element is Visible   xpath=${TC001_FACTURE_DOC}    timeout=${TC001_WAITING_TIMEOUT}
    Wait Until Element is Visible   xpath=${ERC_DOCPAGE_ATTACHMENT_DELETE_TT_BTN}   timeout=${TC001_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${ERC_DOCPAGE_ATTACHMENT_DELETE_TT_BTN}   timeout=${TC001_WAITING_TIMEOUT}
    click element       xpath=${ERC_DOCPAGE_ATTACHMENT_DELETE_TT_BTN}
    click element       xpath=${ERC_DOCPAGE_OVERRIDE_YES_BTN}
    Wait Until Element Is Not Visible   xpath=${TC001_FACTURE_DOC}     timeout=${TC001_WAITING_TIMEOUT}

Click to view the invalid document
    Wait Until Element is Visible   xpath=${TC001_ALERT_INVALID_DOC}   timeout=${TC001_WAITING_TIMEOUT}
    Wait Until Element is Enabled   xpath=${TC001_ALERT_INVALID_DOC}   timeout=${TC001_WAITING_TIMEOUT}
    Scroll Element Into View        xpath=${TC001_ALERT_INVALID_DOC}
    click element   ${TC001_ALERT_INVALID_DOC}

User open again the transaction in Search for Trade Transactions (TT)
    SearchTT_Requests_Page.Click Search link
    SearchTT_Requests_Page.Select Status of Transaction    ${TC001_STATUS}
    SearchTT_Requests_Page.Click Search button
    SearchTT_Requests_Page.Click Edit Icon button

Enter the details on the following mandatory fields
    TT_Document_Page.Click Names & Parties Tab
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_CONTACT}       ${TC001_CONTACT}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_PHONENUM}      ${TC001_PHONE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_APPEMAIL}      ${TC001_EMAIL}
    TT_Document_Page.Click Transport Tab
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_CARRIER}     ${TC001_CARRIER}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_BLNO}          ${TC001_AW_BL_CM_NO}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_DAT_BLDATE}          ${TC001_AW_BL_CM_DATE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_VOYAGENO}      ${TC001_FL_VO_TR_NO}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TRANSPORTNO}   ${TC001_TRANSPORT_NO}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_NETMASS}       ${TC001_NET_MASS}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_GROSSMASS}     ${TC001_GROSS_MASS}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_DISCHARGE}   ${TC001_PLACE_DISCHARGE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTAL_PACKING}  ${TC001_TOTAL_PACKING}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_FCLNO}         ${TC001_FCL}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_LCLNO}         ${TC001_LCL}
    TT_Document_Page.Click Financial Tab
    TT_Document_Page.Select Incoterm per Item    ${TC001_INC}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_TOTALINV}    ${TC001_INV_VALUE}
    TT_Document_Page.Click Goods Tab
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_GDESC}         ${TC001_AUTOGENERATED}
    TT_Document_Page.Click Attachment Tab
    TT_Document_Page.Input Doc Type under Attachments Tab           ${TC001_FF}
    TT_Document_Page.Input Reference Number under Attachments Tab   ${TC001_AUTOGENERATED}
    TT_Document_Page.Upload and Add File under Attachments Tab
    TT_Document_Page.Input Doc Type under Attachments Tab           ${TC001_DAI}
    TT_Document_Page.Input Reference Number under Attachments Tab   ${TC001_AUTOGENERATED}
    TT_Document_Page.Upload and Add File under Attachments Tab
    TT_Document_Page.Input Doc Type under Attachments Tab           ${TC001_TRANSPORT_DOCUMENT}
    TT_Document_Page.Input Reference Number under Attachments Tab   ${TC001_AUTOGENERATED}
    TT_Document_Page.Upload and Add File under Attachments Tab
    TT_Document_Page.Input Doc Type under Attachments Tab           ${TC001_DAF}
    TT_Document_Page.Input Reference Number under Attachments Tab   ${TC001_AUTOGENERATED}
    TT_Document_Page.Upload and Add File under Attachments Tab


User should successfully submitted the transaction with Load TVF Data
    [Documentation]   This keyword is to notify the user if the transaction was successfully submittted
    page should contain                   ${TC001_ALERT_SUBMITTED}
    Page Should Contain Element     xpath=${TC001_STATUS_SUBMITTED}

Verify the entered details on the field was updated
    [Documentation]   This keyword is to verify details were updated on the fields after the operation
    [Arguments]  ${locator}     ${expectedString}
    ${value}=    Get Element Attribute  ${locator}   value
    Should Be Equal     ${value}    ${expectedString}
