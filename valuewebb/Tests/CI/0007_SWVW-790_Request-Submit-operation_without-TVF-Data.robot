*** Settings ***
Documentation    Test Case 007 SWVW-790. This Test Case is used for Submit Trade Transactions without TVF
Library     SeleniumLibrary
Library     BuiltIn
Library     OperatingSystem
Resource    ../../Resources/ERC_Module/ERC_Setup_Teardown.robot
Resource    ../../Resources/ERC_Module/Page_Objects/TT_Document_Page.robot
Resource    ../../Resources/ERC_Module/Page_Objects/SearchTT_Requests_Page.robot
Resource    ../../Resources/ERC_Module/Page_Objects/Login_Page.robot
Suite Setup     User to Open Browser
Suite Teardown    User to Close Browser

*** Variables ***
${TC007_WAITING_TIMEOUT}           20s
${TC007_ALERT_MESSAGE_VERIFIED}    Document is verified!
${TC007_ALERT_MESSAGE_STORED}      Store operation is done.
${TC007_ALERT_MESSAGE_SUBMITTED}   Submit operation is done.
${TC007_STORED_STATUS}             //span[contains(text(), 'Stored')]
${TC007_SUBMITTED_STATUS}          //span[contains(text(), 'Submitted')]
${TC007_SUBMITTED_STATUS_TABLE}    //tbody[@id='searchResBody']//td[@title='Submitted']
${TC007_TAXPAYER_CODE}             5604172W
${TC007_EXP_NAME}                  HMR Trading
${TC007_EXP_ADDRESS}               HY Singa St. Avenue Cmp
${TC007_EXP_CITY}                  New Delhun
${TC007_COUNTRY}                   CI
${TC007_DEC_CODE}                  00069Z
${TC007_CONTACT}                   Edmundo
${TC007_PHONE}                     +22990012356
${TC007_EMAIL}                     dan.bacarra@webbfontaine.com
${TC007_TYPE}                      Partial
${TC007_CARRIER}                   AA
${TC007_AW_BL_CM_NO}               LH02202
${TC007_DATE}                      01/01/2020
${TC007_FL_VO_TR_NO}               PR-256
${TC007_TRANSPORT_NO}              FERM EXPRESS
${TC007_MOT}                       XPR
${TC007_NET_MASS}                  10
${TC007_GROSS_MASS}                10
${TC007_PLACE_LOADING}             CIABO
${TC007_PLACE_DISCHARGE}           CIABJ
${TC007_TOTAL_PACKING}             10
${TC007_FCL}                       0
${TC007_LCL}                       0
${TC007_INV_REF}                   678-110
${TC007_INC}                       FOB
${TC007_CURRENCY}                  XOF
${TC007_VALUE}                     2,000.00
${TC007_MOP}                       10
${TC007_NO_OF_ITEM}                1
${TC007_AUTOGENERATED}             AUTO
${TC007_HSC}                       3002901000
${TC007_QTY}                       5.00
${TC007_UOM}                       KG
${TC007_FF}                        Facture Finale
${TC007_TRANSPORT_DOCUMENT}        Transport Document
${TC007_DAF}                       DAF


*** Test Cases ***
Test Case 007 SWVW-790 Request Submit operation without TVF
    [Tags]    Smoke
    Given Login at ERC Homepage
    When ERC Page should be in English
    Then Detect Language used in ERC Page
    And Click Create TT link
    And Go to Names & Parties Tab to supply the following required fields
    And Go to Transport Tab to supply the following required fields
    And Go to Financial Tab to supply the following required fields
    And Verify the field should be Auto-computed value in Financial Tab
    And Go to Goods Tab to supply the following required fields
    And Go to Remarks Tab to supply the following required fields
    And Go to Attachment Tab to supply the following required fields
    And Click on Verify button to validate all required fields has been correctly supplied
    And Click Store button
    Then Successful Store Operation, messaging should be displayed and TT ID and other details should be provided
    And Searched for the newly created TT ID by entering TT ID on TT field then hit Search button
    And Click Edit Icon button
    And Click on Verify button to validate all required fields has been correctly supplied
    And Click Submit button with Warning Message
    Then User should successfully submitted the transaction
    And Searched for the newly created TT ID by entering TT ID on TT field then hit Search button
    Then Verify that loaded TT ID document will have a Status: Submitted
    Then Verify that there will be no Edit action for the TT ID that has a status of Submitted


*** Keywords ***
User should successfully submitted the transaction
    [Documentation]   This keyword is to notify the user if the transaction was successfully submittted
    page should contain     ${TC007_ALERT_MESSAGE_SUBMITTED}
    Page Should Contain Element  xpath=${TC007_SUBMITTED_STATUS}

Go to Names & Parties Tab to supply the following required fields
    TT_Document_Page.Click Names & Parties Tab
    TT_Document_Page.Enter/Modify Field Details of  xpath=${ERC_DOCPAGE_SUGGEST_IMP}      ${TC007_TAXPAYER_CODE}
    TT_Document_Page.Enter/Modify Field Details of  xpath=${ERC_DOCPAGE_INPUT_EXPNAM}     ${TC007_EXP_NAME}
    TT_Document_Page.Enter/Modify Field Details of  xpath=${ERC_DOCPAGE_INPUT_EXPADD}     ${TC007_EXP_ADDRESS}
    TT_Document_Page.Enter/Modify Field Details of  xpath=${ERC_DOCPAGE_INPUT_EXPCTY}     ${TC007_EXP_CITY}
    TT_Document_Page.Enter/Modify Field Details of  xpath=${ERC_DOCPAGE_SUGGEST_COUNTRY}  ${TC007_COUNTRY}
    TT_Document_Page.Enter/Modify Field Details of  xpath=${ERC_DOCPAGE_SUGGEST_DEC}      ${TC007_DEC_CODE}
    TT_Document_Page.Enter/Modify Field Details of  xpath=${ERC_DOCPAGE_INPUT_CONTACT}    ${TC007_CONTACT}
    TT_Document_Page.Enter/Modify Field Details of  xpath=${ERC_DOCPAGE_INPUT_PHONENUM}   ${TC007_PHONE}
    TT_Document_Page.Enter/Modify Field Details of  xpath=${ERC_DOCPAGE_INPUT_APPEMAIL}   ${TC007_EMAIL}

Go to Transport Tab to supply the following required fields
    TT_Document_Page.Click Transport Tab
    TT_Document_Page.Select Shipment Type   ${TC007_TYPE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_COS}      ${TC007_COUNTRY}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_CARRIER}  ${TC007_CARRIER}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_BLNO}       ${TC007_AW_BL_CM_NO}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_DAT_BLDATE}       ${TC007_DATE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_VOYAGENO}   ${TC007_FL_VO_TR_NO}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TRANSPORTNO}     ${TC007_TRANSPORT_NO}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_MOT}      ${TC007_MOT}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_NETMASS}    ${TC007_NET_MASS}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_GROSSMASS}  ${TC007_GROSS_MASS}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_PLACELOADING}  ${TC007_PLACE_LOADING}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_DISCHARGE}     ${TC007_PLACE_DISCHARGE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTAL_PACKING}   ${TC007_TOTAL_PACKING}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_FCLNO}      ${TC007_FCL}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_LCLNO}      ${TC007_LCL}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_MARKS}      ${TC007_AUTOGENERATED}

Go to Financial Tab to supply the following required fields
    TT_Document_Page.Click Financial Tab
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_INVREF}        ${TC007_INV_REF}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_DAT_INV}             ${TC007_DATE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_CURRENCY}    ${TC007_CURRENCY}
    TT_Document_Page.Select Incoterm    ${TC007_INC}
    TT_Document_Page.Select Incoterm per Item    ${TC007_INC}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTALFOB}      ${TC007_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTALCHARGES}  ${TC007_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTALFREIGHT}  ${TC007_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTALINSURANCE}    ${TC007_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_TOTALDEDUCTIBLES}  ${TC007_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_TOTALINV}        ${TC007_VALUE}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_SUGGEST_PAYMENT}         ${TC007_MOP}

Verify the field should be Auto-computed value in Financial Tab
    TT_Document_Page.Verify Field and Value of   xpath=${ERC_DOCPAGE_INPUT_TOTALCFR}
    TT_Document_Page.Verify Field and Value of   xpath=${ERC_DOCPAGE_INPUT_TOTALCIF}
    TT_Document_Page.Verify Field and Value of   xpath=${ERC_DOCPAGE_INPUT_TOTALVALUE}
    TT_Document_Page.Verify Field and Value of   xpath=${ERC_DOCPAGE_INPUT_TOTALDUTIABLE}

Go to Goods Tab to supply the following required fields
    TT_Document_Page.Click Goods Tab
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_GDESC}  ${TC007_AUTOGENERATED}
    TT_Document_Page.Add Items under Goods Tab              ${TC007_NO_OF_ITEM}
    TT_Document_Page.Enter Product Code under Goods Tab     ${TC007_HSC}
    TT_Document_Page.Enter Product Description under Goods Tab  ${TC007_AUTOGENERATED}
    TT_Document_Page.Enter Incoterm Item value under Goods Tab  ${TC007_VALUE}
    TT_Document_Page.Enter Item Quantity under Goods Tab        ${TC007_QTY}
    TT_Document_Page.Enter Item Unit Measurement under Goods Tab    ${TC007_UOM}
    TT_Document_Page.Enter Item Country Of Origin under Goods Tab   ${TC007_COUNTRY}

Go to Remarks Tab to supply the following required fields
    TT_Document_Page.Click Remarks Tab
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_REMARKS}  ${TC007_AUTOGENERATED}
    TT_Document_Page.Enter/Modify Field Details of     xpath=${ERC_DOCPAGE_INPUT_REASONS}  ${TC007_AUTOGENERATED}

Go to Attachment Tab to supply the following required fields
    TT_Document_Page.Click Attachment Tab
    TT_Document_Page.Input Doc Type under Attachments Tab            ${TC007_FF}
    TT_Document_Page.Input Reference Number under Attachments Tab    ${TC007_AUTOGENERATED}
    TT_Document_Page.Upload and Add File under Attachments Tab
    TT_Document_Page.Input Doc Type under Attachments Tab            ${TC007_TRANSPORT_DOCUMENT}
    TT_Document_Page.Input Reference Number under Attachments Tab    ${TC007_AUTOGENERATED}
    TT_Document_Page.Upload and Add File under Attachments Tab
    TT_Document_Page.Input Doc Type under Attachments Tab            ${TC007_DAF}
    TT_Document_Page.Input Reference Number under Attachments Tab    ${TC007_AUTOGENERATED}
    TT_Document_Page.Upload and Add File under Attachments Tab

Click on Verify button to validate all required fields has been correctly supplied
    TT_Document_Page.Click Verify button
    Wait Until Page Contains    ${TC007_ALERT_MESSAGE_VERIFIED}    timeout=${TC007_WAITING_TIMEOUT}

Successful Store Operation, messaging should be displayed and TT ID and other details should be provided
   Wait Until Page Contains        ${TC007_ALERT_MESSAGE_STORED}    timeout=${TC007_WAITING_TIMEOUT}
   Page Should Contain Element     ${TC007_STORED_STATUS}
   Value of the Element Field  xpath=${ERC_DOCPAGE_TTID_LOCATOR}

Searched for the newly created TT ID by entering TT ID on TT field then hit Search button
   Wait Until Element is Visible    xpath=${ERC_DOCPAGE_TTID_LOCATOR}       timeout=${TC007_WAITING_TIMEOUT}
   element should be visible   xpath=${ERC_DOCPAGE_TTID_LOCATOR}
   ${value}=    Get Text  xpath=${ERC_DOCPAGE_TTID_LOCATOR}
   Should Not Be Empty  ${value}
   SearchTT_Requests_Page.Click Search link
   SearchTT_Requests_Page.Enter Request ID  ${value}

Verify that loaded TT ID document will have a Status: Submitted
   Wait Until Page Contains Element    xpath=${TC007_SUBMITTED_STATUS_TABLE}   timeout=${TC007_WAITING_TIMEOUT}

Verify that there will be no Edit action for the TT ID that has a status of Submitted
    Wait Until Page Does Not Contain Element  xpath=${ERC_SEARCHPAGE_EDIT_TT_BTN}   timeout=${TC007_WAITING_TIMEOUT}
